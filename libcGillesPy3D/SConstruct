import distutils.sysconfig
import os

# Make sure python3 dev headers are installed
# On Ubuntu, python3-dev (might need version-specific, e.g. python3.11-dev, which might require adding new repos)
# On Fedora, python3-devel (might need version-specific, e.g. python3.11-devel)
pyinc = distutils.sysconfig.get_python_inc()

env = Environment(
    CXX="g++",
    CXXFLAGS=[
        "-std=c++17",
        "-fPIC",
    ],
    TOOLS=[
        "default",
        "swig",
    ],
    SWIGOUTDIR=Dir("bin"),
    CPPPATH=[
        Dir("include"),
        Dir("external/ANN/include"),
        Dir("external/Sundials/include"),
        Dir("external/Sundials/src"),
        Dir("external/Sundials/cmake-build/default/include"),
        Dir("external/Sundials/src/sundials"),
        pyinc,
    ],
    LIBPATH=[
        Dir("obj"),
        Dir("obj/external/Sundials"),
    ],
    ENV=os.environ,
)

env.Execute(Mkdir("bin"))

# Send dependency build files to obj/ subfolders
# From this point on, refer to all source files as being relative to the build directory
# SCons will automagically map your build-relative file to the real path
VariantDir("obj/src", "src", duplicate=False)
VariantDir("obj/external", "external", duplicate=False)
VariantDir("obj/include", "include", duplicate=False)

libcgillespy3d = SConscript("obj/src/SConscript", exports=["env"])
sundials = SConscript("obj/external/Sundials/SConscript", exports=["env"])

py_lib = env.SharedLibrary("bin/_libcgillespy3d",
    [
        "obj/include/libcgillespy3d.i",
    ],
    LIBS=[libcgillespy3d, sundials],
    SHLIBPREFIX="",
    SWIGFLAGS=[
        "-c++",
        "-python",
        "-features",
        "autodoc",
    ]
)
env.Clean(py_lib, Dir("bin/__pycache__"))
